buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'

version = '1.0.1'
group = 'top.seraphjack'
archivesBaseName = 'SimpleLogin-1.19'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'official', version: '1.19'
    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            mods {
                simplelogin {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'debug'

            mods {
                simplelogin {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'simplelogin', '--all', '--output', file('src/generated/resources/')

            mods {
                simplelogin {
                    source sourceSets.main
                }
            }
        }
    }
}

configurations {
    shade
    compileOnly.extendsFrom shade
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.19-41.0.64'

    shade 'org.mindrot:jbcrypt:0.4'
}

allprojects {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
    }
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'
        expand 'version': project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
}

tasks.jar.mustRunAfter(processResources)

jar {
    manifest {
        attributes([
                "Specification-Title"     : "SimpleLogin",
                "Specification-Vendor"    : "SeraphJACK",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    from(configurations.shade.collect {
        it.isDirectory() ? it : zipTree(it)
    }) {
        exclude 'META-INF/**'
    }
}
